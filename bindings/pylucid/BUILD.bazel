load("@pypi//:requirements.bzl", "requirement")
load("@rules_python//python:defs.bzl", "py_binary", "py_library")
load("@rules_python//python:packaging.bzl", "py_wheel")
load("//tools:rules_cc.bzl", "lucid_pybind_extension", "lucid_pybind_library")
load("//tools:variables.bzl", "LUCID_AUTHOR", "LUCID_AUTHOR_EMAIL", "LUCID_CLASSIFIERS", "LUCID_DESCRIPTION", "LUCID_HOMEPAGE", "LUCID_LICENSE", "LUCID_SOURCE", "LUCID_TRACKER", "LUCID_VERSION")
load("@aspect_bazel_lib//lib:copy_directory.bzl", "copy_directory")

exports_files(["configuration_schema.json"])

lucid_pybind_library(
    name = "interrupt",
    srcs = ["interrupt.cpp"],
    hdrs = ["interrupt.h"],
    visibility = ["//lucid:__subpackages__"],
)

# The following is a workaround since, on windows, rules_js does not work properly.
# Instead, we expect the frontend to be built with npm and then copied over.
copy_directory(
    name = "frontend",
    src = select(
        {
            "@platforms//os:windows": "//frontend:build",
            "//conditions:default": "//frontend",
        },
    ),
    out = "frontend",
)

lucid_pybind_extension(
    name = "_pylucid",
    srcs = [
        "model.cpp",
        "pylucid.cpp",
        "pylucid.h",
        "util.cpp",
        "verification.cpp",
    ],
    linkstatic = select({
        "//tools:static_build": True,
        "//conditions:default": False,
    }),
    deps = [
        "//lucid:version",
        "//lucid/model",
        "//lucid/util",
        "//lucid/util:error",
        "//lucid/util:logging",
        "//lucid/verification",
    ],
)

py_library(
    name = "pylucid_lib",
    srcs = [
        "__init__.py",
        "__main__.py",
        "cli.py",
        "dreal.py",
        "ext.py",
        "parser.py",
        "pipeline.py",
        "plot.py",
        "util.py",
    ] + select({
        "//tools:gui_build": ["gui.py"],
        "//conditions:default": [],
    }),
    data = [
        ":_pylucid.pyd",
        ":_pylucid.so",
        ":configuration_schema.json",
    ] + select({
        "//tools:gui_build": [":frontend"],
        "//conditions:default": [],
    }),
    imports = [".."],
    srcs_version = "PY3",
    visibility = ["//visibility:public"],
    deps = [
        requirement("numpy"),
        requirement("scipy"),
        requirement("pyparsing"),
        requirement("sympy"),
        requirement("pyyaml"),
        requirement("jsonschema"),
    ],
)

py_binary(
    name = "pylucid",
    srcs = ["__main__.py"],
    imports = [".."],
    main = "__main__.py",
    python_version = "PY3",
    deps = [
        requirement("numpy"),
        ":pylucid_lib",
    ],
)

py_binary(
    name = "stubgen_bin",
    srcs = ["_stubgen.py"],
    imports = [".."],
    main = "_stubgen.py",
    python_version = "PY3",
    deps = [
        requirement("pybind11_stubgen"),
        requirement("numpy"),
        requirement("scipy"),
        requirement("pyparsing"),
        requirement("sympy"),
        requirement("pyyaml"),
        requirement("jsonschema"),
    ],
)

genrule(
    name = "stubgen",
    srcs = [
        ":pylucid_lib",
        ":_pylucid.pyd",
        ":_pylucid.so",
    ],
    outs = [
        "_pylucid/__init__.pyi",
        "_pylucid/random.pyi",
        "_pylucid/exception.pyi",
        "_pylucid/log.pyi",
        "__init__.pyi",
        "py.typed",
    ],
    cmd = "$(location :stubgen_bin) --pyd $(location :_pylucid.pyd) --so $(location :_pylucid.so) --lib $(locations :pylucid_lib) --outs $(OUTS)",
    cmd_bat = "$(location :stubgen_bin) --pyd $(location :_pylucid.pyd) --so $(location :_pylucid.so) --lib $(locations :pylucid_lib) --outs $(OUTS)",
    tools = [":stubgen_bin"],
)

filegroup(
    name = "pylucid_files",
    srcs = [
        "configuration_schema.json",
        ":_pylucid",
        ":pylucid_lib",
        ":stubgen",
    ] + select({
        "//tools:gui_build": [":frontend"],
        "//conditions:default": [],
    }),
)

py_wheel(
    name = "pylucid_wheel",
    abi = "$(PYTHON_ABI_TAG)",
    author = LUCID_AUTHOR,
    author_email = LUCID_AUTHOR_EMAIL,
    classifiers = LUCID_CLASSIFIERS,
    description_content_type = "text/markdown",
    description_file = "//:README.md",
    distribution = "pylucid",
    entry_points = {
        "console_scripts": ["pylucid = pylucid.__main__:main"],
    },
    homepage = LUCID_HOMEPAGE,
    license = LUCID_LICENSE,
    platform = select({
        "@bazel_tools//src/conditions:windows_x64": "win_amd64",
        "@bazel_tools//src/conditions:windows_arm64": "win_arm64",
        "@bazel_tools//src/conditions:darwin_x86_64": "macosx_12_0_x86_64",
        "@bazel_tools//src/conditions:darwin_arm64": "macosx_12_0_arm64",
        "@bazel_tools//src/conditions:linux_x86_64": "manylinux_2_34_x86_64",
        "@bazel_tools//src/conditions:linux_aarch64": "manylinux_2_34_aarch64",
    }),
    project_urls = {
        "Source": LUCID_SOURCE,
        "Tracker": LUCID_TRACKER,
    },
    python_tag = "$(PYTHON_TAG)",
    summary = LUCID_DESCRIPTION,
    toolchains = ["//tools:make_var_substitution"],
    version = LUCID_VERSION,
    deps = [":pylucid_files"],
)
