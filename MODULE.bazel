"""lucid module"""

module(name = "lucid")

#################################################################################
# Available dependencies from the Bazel registry (https://registry.bazel.build/)
#################################################################################

# Apache License 2.0 | rules_cc for building C++ libraries with Bazel
bazel_dep(name = "rules_cc", version = "0.1.2")

# Apache License 2.0 | utility rules for Bazel
bazel_dep(name = "bazel_skylib", version = "1.8.1")

# Apache License 2.0 | utility rules for Bazel
bazel_dep(name = "aspect_bazel_lib", version = "2.19.4")

# Apache License 2.0 | rules housing all canonical settings for cross-platform builds
bazel_dep(name = "platforms", version = "1.0.0")

# Mozilla Public License 2.0 | C++ library that handles matrices and vectors
bazel_dep(name = "eigen", version = "3.4.0.bcr.3")

# MIT | argparse implementation for modern C++
bazel_dep(name = "argparse", version = "3.2.0")

# MIT | C++ library for formatting text
bazel_dep(name = "fmt", version = "11.2.0")

# MIT | logging library
bazel_dep(name = "spdlog", version = "1.15.2")

# MIT | Linear optimization software
bazel_dep(name = "highs", version = "1.11.0")
git_override(
    module_name = "highs",
    commit = "340f0982e2587628608549d4891420c7304057d3",
    remote = "https://github.com/ERGO-Code/HiGHS.git",
)

# Apache License 2.0 | Linear programming solver
bazel_dep(name = "soplex", version = "7.1.4.bcr.1")

# https://zlib.net/zlib_license.html | Compression library
bazel_dep(name = "zlib", version = "1.3.1.bcr.7")

# https://www.boost.org/users/license.html | Boost C++ Libraries for math
bazel_dep(name = "boost.math", version = "1.87.0")

# Apache License 2.0 | rules_python for building python targets
bazel_dep(name = "rules_python", version = "1.4.1")

# https://github.com/pybind/pybind11_bazel/blob/master/LICENSE | pybind11 for binding C++ and Python
bazel_dep(name = "pybind11_bazel", version = "2.13.6")

# Apache License 2.0 | Bazel rules for packaging the project
bazel_dep(name = "rules_pkg", version = "1.1.0")

# Apache License 2.0 | Bazel rules for shell scripts
bazel_dep(name = "rules_shell", version = "0.6.1")

# Apache License 2.0 | Node.js toolchain for Bazel
bazel_dep(name = "rules_nodejs", version = "6.4.0")

# Apache License 2.0 | rules for running javascript applications in Bazel
bazel_dep(name = "aspect_rules_js", version = "2.0.2")

# https://github.com/cpplint/cpplint/blob/develop/LICENSE | Google C++ Style Guide
bazel_dep(name = "cpplint", version = "2.0.2", dev_dependency = True)

# BSD-3-Clause license | C++ test suite by Google
bazel_dep(name = "googletest", version = "1.17.0", dev_dependency = True)

# Apache License 2.0 | Doxygen documentation generator
bazel_dep(name = "rules_doxygen", version = "2.4.0", dev_dependency = True)

# MIT | rules for dealing with target dependencies
bazel_dep(name = "depend_on_what_you_use", version = "0.9.0", dev_dependency = True)

#################################################################################
# External dependencies not available in the Bazel registry
#################################################################################

http_archive = use_repo_rule("@bazel_tools//tools/build_defs/repo:http.bzl", "http_archive")

# GPL 2+ | alglib library for numerical analysis and data processing
http_archive(
    name = "alglib",
    build_file = "//third_party:alglib.BUILD.bazel",
    sha256 = "a4b6f64cac2d5340cf507569db13d1178ce3788d9dda0cb1dba90666e619f3a7",
    strip_prefix = "alglib-cpp",
    url = "https://www.alglib.net/translator/re/alglib-4.05.0.cpp.gpl.zip",
)

# Gurobi installation. Must be already installed on the system.

gurobi_repository = use_repo_rule("//tools:gurobi_build.bzl", "gurobi_repository")

gurobi_repository(
    name = "gurobi",
    build_file = "//third_party:gurobi.BUILD.bazel",
    default_gurobi_home = "/opt/gurobi1201/linux64",  # Replace with your Gurobi installation path. It will be used if the environment variable GUROBI_HOME is not set.
)

# GitHub repositories

github_archive = use_repo_rule("//tools:git_archive.bzl", "github_archive")

# MIT | numerical optimization library
github_archive(
    name = "lbfgscpp",
    build_file = "//third_party:lbfgscpp.BUILD.bazel",
    commit = "v0.4.0",
    repository = "yixuan/LBFGSpp",
    sha256 = "39c4aaebd8b94ccdc98191d51913a31cddd618cc0869d99f07a4b6da83ca6254",
)

# MIT | fast fourier transform library
github_archive(
    name = "pocketfft",
    build_file = "//third_party:pocketfft.BUILD.bazel",
    commit = "0fa0ef591e38c2758e3184c6c23e497b9f732ffa",
    repository = "mreineck/pocketfft",
    sha256 = "1cfd0494ac6fe88e034579cb655b0ef9731b39a0432558e50fe41c4abd6d8684",
)

# MIT | polytopic computation library
github_archive(
    name = "hops",
    build_file = "//third_party:hops.BUILD.bazel",
    commit = "19fdc4c60701f5081ff501f47068c28a477e7fdc",
    repository = "modsim/hops",
    sha256 = "4eb38d42fb8652b153416fca1ed2c64bda69075c0956568da77c4383fd341467",
)

#################################################################################
# Load module extensions
#################################################################################
doxygen_extension = use_extension("@rules_doxygen//:extensions.bzl", "doxygen_extension")
doxygen_extension.configuration(
    platform = "linux",
    sha256 = "f2c0a349403bc5b5ade3f501301e32b49ea31b3182666954bc398452fbc0dd1c",
    version = "1.13.2",
)
use_repo(doxygen_extension, "doxygen")

node = use_extension("@rules_nodejs//nodejs:extensions.bzl", "node")
node.toolchain(node_version = "20.19.2")

npm = use_extension("@aspect_rules_js//npm:extensions.bzl", "npm")
npm.npm_translate_lock(
    name = "npm",
    data = [
        "//frontend:package.json",
    ],
    lifecycle_hooks_exclude = [
        "@swc/core",
        "@tailwindcss/oxide",
        "esbuild",
    ],
    npmrc = "//frontend:.npmrc",
    pnpm_lock = "//frontend:pnpm-lock.yaml",
    verify_node_modules_ignored = "//:.bazelignore",
    # update_pnpm_lock = 1, # Only needed if you want to update the pnpm lock file
)
use_repo(npm, "npm")

python = use_extension("@rules_python//python/extensions:python.bzl", "python")
python.toolchain(
    python_version = "3.8",
)
python.toolchain(
    python_version = "3.9",
)
python.toolchain(
    is_default = True,
    python_version = "3.10",
)
python.toolchain(
    python_version = "3.11",
)
python.toolchain(
    python_version = "3.12",
)
python.toolchain(
    python_version = "3.13",
)
use_repo(
    python,
    "python_3_10",
    "python_3_11",
    "python_3_12",
    "python_3_13",
    "python_3_8",
    "python_3_9",
    "python_versions",
)

pip = use_extension("@rules_python//python/extensions:pip.bzl", "pip")
pip.parse(
    hub_name = "pypi",
    python_version = "3.8",
    requirements_lock = "//tools:requirements.txt",
)
pip.parse(
    hub_name = "pypi",
    python_version = "3.9",
    requirements_lock = "//tools:requirements.txt",
)
pip.parse(
    hub_name = "pypi",
    python_version = "3.10",
    requirements_lock = "//tools:requirements.txt",
)
pip.parse(
    hub_name = "pypi",
    python_version = "3.11",
    requirements_lock = "//tools:requirements.txt",
)
pip.parse(
    hub_name = "pypi",
    python_version = "3.12",
    requirements_lock = "//tools:requirements.txt",
)
pip.parse(
    hub_name = "pypi",
    python_version = "3.13",
    requirements_lock = "//tools:requirements.txt",
)
use_repo(pip, "pypi")
