load("@npm//frontend:vite/package_json.bzl", vite_bin = "bin")
load("@npm//:defs.bzl", "npm_link_all_packages")

exports_files([
    ".npmrc",
    "package.json",
    "pnpm-lock.yaml",
])

npm_link_all_packages()

FILE_DEPS = [
    "index.html",
    "package.json",
    "pnpm-lock.yaml",
    "tsconfig.app.json",
    "tsconfig.json",
    "tsconfig.node.json",
    "vite.config.ts",
] + glob([
    "src/**",
    "public/**",
])

BUILD_DEPS = [":node_modules/" + d for d in [
    "zod",
    "@headlessui/react",
    "@hookform/error-message",
    "@hookform/resolvers",
    "@tailwindcss/vite",
    "plotly.js-dist-min",
    "react",
    "react-dom",
    "react-hook-form",
    "react-icons",
    "tailwindcss",
    "vite",
    "vite-tsconfig-paths",
    "@vitejs/plugin-react-swc",
    "@eslint/js",
    "@types/react",
    "@types/react-dom",
    "@types/plotly.js",
    "eslint",
    "eslint-plugin-react-hooks",
    "eslint-plugin-react-refresh",
    "globals",
    "typescript",
    "typescript-eslint",
]]

vite_bin.vite(
    name = "frontend",
    srcs = FILE_DEPS + BUILD_DEPS,
    args = ["build"],
    chdir = package_name(),
    out_dirs = ["dist"],
    visibility = ["//visibility:public"],
)

vite_bin.vite_binary(
    name = "dev",
    chdir = package_name(),
    data = FILE_DEPS + BUILD_DEPS,
    # Under ibazel, let vite hot-reload changed files rather than restart it
    tags = ["ibazel_notify_changes"],
)

filegroup(
    name = "build",
    srcs = ["dist"],
    visibility = ["//visibility:public"],
)
